reset

set terminal pngcairo size  1280, 480
set output 'pvalue_quantiles.png'


set grid
set tics scale 2
unset key

set multiplot layout 1,2

set logscale

set format y "10^{%T}"


################ CUSTOMIZE #####################

cdf(x) = -(x**(-1)-1) # the expected distribution
#cdf(x) = x

N = 42 # the number of samples

set xrange [1:300]
set yrange [.02:1.3]

#################################################


#CDF of detected sources with Monte-Carlo credible intervals from inferred power law
set xlabel 'P ({/Symbol m}N s)'
set ylabel 'cdf'

plot \
        'pvalue_quantiles.dat' u 2:1 w l lw 2 lc 'light-blue',\
	'pvalue_quantiles.dat' u 3:1 w l lw 2 lc 'light-blue',\
	'pvalue_quantiles.dat' u 4:1 w l lw 2 lc 'blue', \
	'pvalue_quantiles.dat' u 5:1 w l lw 2 lc 'blue',\
	'pvalue_quantiles.dat' u 6:1 w l lw 2 lc 'dark-blue',\
	'pvalue_quantiles.dat' u 7:1 w l lw 2 lc 'dark-blue',\
	cdf(x) w l lw 2 lc 'goldenrod' dashtype 2,\
	'<sort -g mean_var.txt' u 1:($0/N):(sqrt($2)) w xerrorbars ls 6 ps 1 lw 1 lc 'red' 

#1-CDF of detected sources with Monte-Carlo credible intervals from inferred power law
set xlabel 'P ({/Symbol m}N s)' 
set ylabel '1-cdf'

plot \
	'pvalue_quantiles.dat' u 2:((1-$1)) w l lw 2 lc 'light-blue', \
	'pvalue_quantiles.dat' u 3:((1-$1)) w l lw 2 lc 'light-blue',\
	'pvalue_quantiles.dat' u 4:((1-$1)) w l lw 2 lc 'blue', \
	'pvalue_quantiles.dat' u 5:((1-$1)) w l lw 2 lc 'blue',\
	'pvalue_quantiles.dat' u 6:((1-$1)) w l lw 2 lc 'dark-blue',\
	'pvalue_quantiles.dat' u 7:((1-$1)) w l lw 2 lc 'dark-blue',\
	1-cdf(x) w l lw 2 lc 'goldenrod' dashtype 2,\
	'<sort -g mean_var.txt' u 1:((N-$0)/N):(sqrt($2)) w xerrorbars ls 6 ps 1 lw 1 lc 'red'

unset multiplot
